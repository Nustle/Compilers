Spec ->
  "%class" IDENT
  "%tokens" TokDef
  "%types" TypesDef
  "%methods" MethodsDef
  "%grammar" GrammarDef
  "%axiom" IDENT
  "%end"

TokDef -> IDENT TokDef | IDENT
TypesDef -> TypeDecls ":" IDENT ";"
TypeDecls -> TypeDecls "," IDENT | IDENT
MethodsDef -> MethodsDef Method | Method
Method -> IDENT IDENT "(" ParamsList ")" ";"

ParamsList -> Params | ε
Params -> Params "," TypeSpec | TypeSpec
TypeSpec -> IDENT "[]" | IDENT

GrammarDef -> GrammarDef GrammarRule | ε
GrammarRule -> IDENT "=" RHSList ";"

RHSList -> RHSList "|" RHS | RHS
RHS -> Sequence ActionOpt
Sequence -> Sequence Element | ε
Element -> RepOpt ElementCore
ElementCore -> "(" RHSList ")" | IDENT
RepOpt -> "%rep" | ε
ActionOpt  -> "/" IDENT | ε